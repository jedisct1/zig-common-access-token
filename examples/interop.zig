const std = @import("std");
const Io = std.Io;
const cat = @import("cat");

pub fn main() !void {
    var gpa = std.heap.GeneralPurposeAllocator(.{}){};
    defer _ = gpa.deinit();
    const allocator = gpa.allocator();

    var threaded: Io.Threaded = .init(allocator, .{});
    defer threaded.deinit();
    const io = threaded.io();

    var arena = std.heap.ArenaAllocator.init(allocator);
    defer arena.deinit();
    const temp_allocator = arena.allocator();

    const key_hex = "403697de87af64611c1d32a05dab0fe1fcb715a86ab435f1ec99192d79569388";
    const key = try cat.util.hexToBytes(temp_allocator, key_hex);

    var keys = std.StringHashMap([]const u8).init(temp_allocator);
    try keys.put("Symmetric256", key);

    const cat_options = cat.CatOptions{
        .keys = keys,
        .expect_cwt_tag = true,
    };

    var cat_instance = cat.Cat.init(allocator, cat_options);
    defer cat_instance.deinit();

    const args = try std.process.argsAlloc(std.heap.page_allocator);
    defer std.process.argsFree(std.heap.page_allocator, args);

    var stdout_buffer: [4096]u8 = undefined;
    var stdout_writer: Io.File.Writer = .init(.stdout(), io, &stdout_buffer);
    const stdout = &stdout_writer.interface;

    if (args.len < 2) {
        try stdout.print("Generating a token to be validated by the NodeJS implementation...\n", .{});

        var claims = cat.Claims.init(temp_allocator);

        try claims.setIssuer("eyevinn");
        try claims.setSubject("user123");
        try claims.setAudience("example");

        const now = cat.util.currentTimeSecs();
        try claims.setExpiration(now + 300);
        try claims.setIssuedAt(now);

        const token = try cat_instance.generate(claims, .{
            .validation_type = cat.CatValidationType.Mac,
            .alg = "HS256",
            .kid = "Symmetric256",
            .generate_cwt_id = true,
        });
        defer allocator.free(token);

        try stdout.print("Generated token: {s}\n", .{token});
        try stdout.print("\nTo validate with node-cat:\n", .{});
        try stdout.print("  npm install @eyevinn/cat\n", .{});
        try stdout.print("  node -e \"const {{ Cat }} = require('@eyevinn/cat'); const cat = new Cat({{ keys: {{ 'Symmetric256': Buffer.from('{s}', 'hex') }} }}); console.log(cat.validate('{s}', {{ issuer: 'eyevinn' }}))\"\n", .{ key_hex, token });
    } else {
        try stdout.print("Validating a token generated by the NodeJS implementation...\n", .{});

        const token = args[1];

        var claims = cat_instance.validate(token, cat.CatValidationType.Mac, .{
            .issuer = "eyevinn",
            .audience = null,
        }) catch |err| {
            try stdout.print("Error validating token: {any}\n", .{err});
            try stdout.flush();
            return;
        };
        defer claims.deinit();

        try stdout.print("Token is valid!\n", .{});

        if (claims.getIssuer()) |issuer| {
            try stdout.print("Issuer: {s}\n", .{issuer});
        }

        if (claims.getSubject()) |subject| {
            try stdout.print("Subject: {s}\n", .{subject});
        }

        if (claims.getAudience()) |audience| {
            try stdout.print("Audience: {s}\n", .{audience});
        }
    }

    try stdout.flush();
}
